plugins {
    id 'java-library'
    id "com.jfrog.bintray" version "1.8.4"     
}

apply plugin: 'maven-publish'
apply from: "gradle/versioning.gradle"

group = 'ch.so.agi.camel'

ext {
    apacheCamelVersion = "2.23.1"
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://jars.interlis.ch" }     
}

dependencies {
    api 'org.apache.commons:commons-math3:3.6.1'
    api "org.apache.camel:camel-core:${apacheCamelVersion}"
    api "org.apache.camel:camel-test:${apacheCamelVersion}"
    api "ch.interlis:ilivalidator:1.11.0"
    api "ch.so.agi.av:av2ch:1.0.6"
    testImplementation 'junit:junit:4.12'
}

// Bintray / jcenter
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def githubRepoUrl = "https://github.com/edigonzales/camel-components"
def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://github.com/edigonzales/camel-components/blob/master/LICENSE"
        }
    }
    developers {
        developer {
            id "edigonzales"
            name "Stefan Ziegler"
            email "stefan.ziegler@bd.so.ch"
        }
    }

    scm {
        url githubRepoUrl
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId project.group
            artifactId 'camel-components'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Processors and Predicates for Apache Camel.')
                root.appendNode('name', 'camel-components')
                root.appendNode('url', githubRepoUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.env.bintrayUser
    key = System.env.bintrayKey
    publications = ['mavenPublication']
    publish = true
    
    pkg {
        repo = 'camel-components'
        name = 'camel-components'
        userOrg = 'sogis'
        licenses = ['MIT']
        labels = ['apache', 'camel', 'apache camel', 'sogis', 'interlis']        
        vcsUrl = githubRepoUrl
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }
}
